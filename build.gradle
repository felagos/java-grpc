plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.gcrp'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

ext {
	grpcVersion = '1.72.0'
	protocVersion = '3.25.5'
	jacksonVersion = '2.18.1'
	tomcatAnnotationsVersion = '6.0.53'
	pgvVersion = '1.0.4'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"

	implementation "build.buf.protoc-gen-validate:pgv-java-stub:${pgvVersion}"
	
	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	implementation 'ch.qos.logback:logback-classic'

	implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protocVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
		javapgv {
			artifact = "build.buf.protoc-gen-validate:protoc-gen-validate:${pgvVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
			javapgv {
				option "lang=java"
			}
		}
	}
}

sourceSets {
	main {
		proto {
			srcDir 'src/main/proto'
		}
	}
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
