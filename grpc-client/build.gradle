plugins {
	id 'java'
	id 'application'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.grpc'
version = '0.0.1-SNAPSHOT'
description = 'gRPC Client Application'

application {
	mainClass = 'com.grpc.course.ClientApplication'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

ext {
	grpcVersion = '1.72.0'
	protocVersion = '3.25.5'
	jacksonVersion = '2.18.1'
	pgvVersion = '1.0.4'
}

dependencies {
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"

	implementation "build.buf.protoc-gen-validate:pgv-java-stub:${pgvVersion}"
	
	implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
	compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

	implementation 'org.slf4j:slf4j-api:2.0.9'
	implementation 'ch.qos.logback:logback-classic:1.4.14'

	implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
	
	implementation 'com.google.guava:guava:33.0.0-jre'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protocVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
		javapgv {
			artifact = "build.buf.protoc-gen-validate:protoc-gen-validate:${pgvVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
			javapgv {
				option "lang=java"
			}
		}
		all().each { task ->
			task.builtins {
				java {
					option 'annotate_code=false'
				}
			}
			task.plugins {
				grpc {
					option 'annotate_code=false'
				}
			}
		}
	}
}

sourceSets {
	main {
		proto {
			srcDir 'src/main/proto'
		}
	}
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

task runUnaryClientAlive(type: JavaExec) {
	group = 'application'
	description = 'Run UnaryClientAlive'
	classpath = sourceSets.main.runtimeClasspath
	mainClass = 'com.grpc.course.UnaryClientAlive'
}
