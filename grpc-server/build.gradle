plugins {
	id 'java'
	id 'application'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.grpc'
version = '0.0.1-SNAPSHOT'
description = 'gRPC Server Application'

application {
	mainClass = 'com.grpc.course.ServerApplication'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

ext {
	grpcVersion = '1.74.0'
	protocVersion = '3.25.5'
	jacksonVersion = '2.18.2'
	tomcatAnnotationsVersion = '6.0.53'
	pgvVersion = '1.0.4'
	protovalidateVersion = '1.1.0'
}

dependencies {
	implementation project(':grpc-shared')
	
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"

	implementation "build.buf.protoc-gen-validate:pgv-java-stub:${pgvVersion}"
	implementation "build.buf:protovalidate:${protovalidateVersion}"
	
	implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
	compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

	implementation 'org.slf4j:slf4j-api:2.0.16'
	implementation 'ch.qos.logback:logback-classic:1.5.15'

	implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

	implementation 'io.netty:netty-handler:4.1.118.Final'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protocVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
		javapgv {
			artifact = "build.buf.protoc-gen-validate:protoc-gen-validate:${pgvVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
			javapgv {
				option "lang=java"
			}
		}
		all().each { task ->
			task.builtins {
				java {
					option 'annotate_code=false'
				}
			}
			task.plugins {
				grpc {
					option 'annotate_code=false'
				}
			}
		}
	}
}

sourceSets {
	main {
		proto {
			srcDir 'src/main/proto'
		}
	}
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jar {
	manifest {
		attributes(
			'Main-Class': 'com.grpc.course.ServerApplication'
		)
	}
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
