plugins {
	id 'java'
	id 'com.google.protobuf' version '0.9.5'
}

group = 'com.grpc'
version = '0.0.1-SNAPSHOT'
description = 'gRPC Shared Proto Definitions'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

ext {
	grpcVersion = '1.58.0'
	protocVersion = '3.25.5'
	pgvVersion = '1.0.4'
	protovalidateVersion = '1.1.0'
}

dependencies {
	
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"

	implementation "build.buf.protoc-gen-validate:pgv-java-stub:${pgvVersion}"
	implementation "build.buf:protovalidate:${protovalidateVersion}"
	
	implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
	compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protocVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
		javapgv {
			artifact = "build.buf.protoc-gen-validate:protoc-gen-validate:${pgvVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
			javapgv {
				option "lang=java"
			}
		}
		all().each { task ->
			task.builtins {
				java {
					option 'annotate_code=false'
				}
			}
			task.plugins {
				grpc {
					option 'annotate_code=false'
				}
			}
		}
	}
}

sourceSets {
	main {
		proto {
			srcDir 'src/main/proto'
		}
	}
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
